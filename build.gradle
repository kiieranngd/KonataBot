import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '1.2.4'
    id 'com.sedmelluq.jdaction' version '1.0.1'
}

mainClassName = "br.net.brjdevs.steven.konata.KonataBot"

group 'br.net.brjdevs.steven'
def ver = new Version(major: 1, minor: 2, revision: 9)
version ver.toString()

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile 'com.google.guava:guava:21.0'
    compile 'com.rethinkdb:rethinkdb-driver:2.3.3'
    compile 'net.dv8tion:JDA:3.0.0_191'
    compile 'com.sedmelluq:lavaplayer:1.2.36'
    compile 'org.reflections:reflections:0.9.10'
    compile 'ch.qos.logback:logback-classic:0.9.26'
    compile 'br.com.brjdevs:utils:1.0.0_4'
    compile 'br.com.brjdevs:snowflakes:1.0.0_1'
    compile 'com.fathzer:javaluator:3.0.0'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.0.pr1'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2'
}

task prepareSource(type: Copy) {
    from 'src/main/java'
    into 'build/prepared-src'
    filter(ReplaceTokens, tokens: [
            version: ver.toString()
    ])
    dependsOn clean
}
prepareSource.dependsOn clean

compileJava {
    source = prepareSource.destinationDir
    classpath = sourceSets.main.compileClasspath
    options.encoding = 'UTF-8'
    dependsOn prepareSource
}



class Version {
    String major, minor, revision

    static String getBuild() {
        return new Date().format('ddMMyyyy')
    }

    String toString() {
        "${major}.${minor}.${revision}_D$build"
    }
}
